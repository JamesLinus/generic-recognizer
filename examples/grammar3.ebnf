!
! Grammar for grammars as accepted by the recognizer.
! The example input string for this grammar (in 'string3')
! is just this file without this comment (in order to feed the
! recognizer with this same file, lex() should be modified to
! accept ! comments).
!

grammar* = rule { rule } "." ;
rule = #ID [ "*" ] "=" expr ";" ;
expr = term { "|" term } ;
term = factor { factor } ;
factor = #ID [ ">" "$" #ID ]
       | "#" #ID
       | #STR2
       | "(" expr ")"
       | "{" expr "}"
       | "[" expr "]"
       | "[[" expr "]]"
       | output
       | control ;
output = "{{" outexpr { outexpr } "}}" ;
outexpr = #STR2 | "*" | "#" | "$" #ID | ";" | "+" | "-" ;
control = "$" ( "push" | "pop" | "eout" | "dout" ) ;

.
