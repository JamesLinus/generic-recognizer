!
! Grammar for grammars as accepted by the recognizer.
! The example input string for this grammar (in 'string3')
! is just this file without this comment (in order to feed the
! recognizer with this same file, lex() should be modified to
! accept ! comments).
!

grammar* = rule , { rule } , dot ;
rule = id , [ star ] , eq , ( nonterm | term ) , semi ;
nonterm = option , { vbar , option } ;
option = concat , { comma , concat } ;
concat = id | lparen , nonterm , rparen | lbrace , nonterm , rbrace | lbracket , nonterm , rbracket ;
term = hash , num ;

eq = #9;
id = #29;
num = #30;
dot = #28;
semi = #23;
comma = #24;
star = #4;
lparen = #26;
rparen = #27;
hash = #10;
vbar = #31;
lbrace = #32;
rbrace = #33;
lbracket = #34;
rbracket = #35;

.
