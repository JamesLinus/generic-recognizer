!
! Grammar for Wirth's PL/0 programming language.
!

program* = block "." ;
block = [ "const" #ID "=" #NUM { "," #ID "=" #NUM } ";" ]
        [ "var" #ID { "," #ID } ";" ]
        { "procedure" #ID ";" block ";" }
        statement ;
statement = [ #ID ":=" expression |
             "call" #ID |
             "begin" statement { ";" statement } "end" |
             "if" condition "then" statement |
             "while" condition "do" statement ] ;
condition = "odd" expression |
            expression ("="|"#"|"<"|">"|"<="|">=") expression ;
expression = ["+"|"-"] term { ("+"|"-") term } ;
term = factor { ("*"|"/") factor } ;
factor = #ID | #NUM | "(" expression ")" ;

.
